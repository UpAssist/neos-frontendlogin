UpAssist.Neos.FrontendLogin.UserController.show = Neos.Fusion:Component {

  labels = Neos.Fusion:DataStructure {
    username = ${I18n.translate('UpAssist.Neos.FrontendLogin:Main:label.username')}
    password = ${I18n.translate('UpAssist.Neos.FrontendLogin:Main:label.password.change')}
    firstname = ${I18n.translate('UpAssist.Neos.FrontendLogin:Main:label.firstname')}
    email = ${I18n.translate('UpAssist.Neos.FrontendLogin:Main:label.email')}
    lastname = ${I18n.translate('UpAssist.Neos.FrontendLogin:Main:label.lastname')}
    submit = ${I18n.translate('UpAssist.Neos.FrontendLogin:Main:button.update')}
  }

  renderer = afx`
    <Neos.Fusion.Form:Form
        form.data.user={user}
        form.target.controller="User"
        form.target.action="update"
    >
      <fieldset>
        <label for="username">{props.labels.username}</label>
        <input type="text" readonly disabled value={user.accounts[0].accountIdentifier} />
      </fieldset>
      <fieldset class="user-name">
        <Neos.Fusion.Form:FieldContainer field.name="user[name][firstName]" label={props.labels.firstname}>
          <Neos.Fusion.Form:Input/>
        </Neos.Fusion.Form:FieldContainer>
        <Neos.Fusion.Form:FieldContainer field.name="user[name][lastName]" label={props.labels.lastname}>
          <Neos.Fusion.Form:Input/>
        </Neos.Fusion.Form:FieldContainer>
      </fieldset>
      <fieldset>
        <Neos.Fusion.Form:FieldContainer field.name="user[electronicAddresses][0][identifier]" label={props.labels.email}>
          <Neos.Fusion.Form:Input />
        </Neos.Fusion.Form:FieldContainer>
        <Neos.Fusion.Form:Hidden field.name="user[electronicAddresses][0][type]" field.value="Email"/>
      </fieldset>
      <fieldset>
        <Neos.Fusion.Form:FieldContainer field.name="password" label={props.labels.password}>
          <Neos.Fusion.Form:Input />
        </Neos.Fusion.Form:FieldContainer>
      </fieldset>
      <Neos.Fusion.Form:Button type="submit">{props.labels.submit}</Neos.Fusion.Form:Button>
    </Neos.Fusion.Form:Form>
  `
}
